buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.1'
    }
}

apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'

group = 'com.github.shendong.gradle'
version = '1.0.0'

description = 'A plugin for integrating with Coverity & Fortify. ' +
        'For Coverity, adds tasks to the project that will emit, analyze and commit data.' +
        'For Fortify, adds tasks to the project that will transit java code.' +
		'This plugin supports analysis of Java code for Java and/or Android projects.'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:1.0.0'
}

pluginBundle {
    website = 'https://github.com/shendong/CoverityFortify-plugin'
    vcsUrl = 'https://github.com/shendong/CoverityFortify-plugin'
    description = project.description
    tags = ['coverity', 'java', 'android']

    plugins {
        coverityPlugin {
            id = 'com.github.sdong.gradle.coverityfortify'
            displayName = 'Gradle Coverity & Fortify Plugin'
        }
    }
}

task checkJavaVersion << {
    if (!JavaVersion.current().isJava7()) {
        throw new GradleException(
                "JDK 1.7 is required (found ${JavaVersion.current()}).")
    }
}

compileJava.dependsOn checkJavaVersion

compileGroovy {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-Xlint:all']
    options.deprecation = true
    options.warnings = true
}
